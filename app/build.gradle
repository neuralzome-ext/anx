plugins {
    id 'kotlin-kapt'
    id 'com.android.application'
    id 'dagger.hilt.android.plugin'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdk 32

    signingConfigs {

        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())

        def KEYSTORE_FILE_PATH = "KEYSTORE_FILE_PATH"
        def KEYSTORE_PASSWORD = "KEYSTORE_PASSWORD"
        def KEY_ALIAS = "KEY_ALIAS"
        def KEY_PASSWORD = "KEY_PASSWORD"

        flo {
            storeFile file(properties.getProperty(KEYSTORE_FILE_PATH))
            storePassword properties.getProperty(KEYSTORE_PASSWORD)
            keyAlias properties.getProperty(KEY_ALIAS)
            keyPassword properties.getProperty(KEY_PASSWORD)
        }
    }

    defaultConfig {
        applicationId "com.flomobility.hermes"
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "0.1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags '-std=c++17'
            }
        }
    }

    buildTypes {
        dev {
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.flo
            versionNameSuffix "-dev"
            resValue "string", "app_name", "(D)Hermes"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.18.1'
        }
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.3.1"

    //Multidex
    implementation 'com.android.support:multidex:1.0.3'

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1'

    // ZMQ - JAVA
    implementation 'org.zeromq:jeromq:0.5.2'

    // Dagger-Hilt
    implementation "com.google.dagger:hilt-android:2.42"
    kapt "com.google.dagger:hilt-compiler:2.42"

    // serialization
    implementation 'com.google.code.gson:gson:2.8.9'

    // usb serial
    implementation 'com.github.felHR85:UsbSerial:6.1.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}